plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

// Versioning
def versionNumber = '1.1.0'
def versionType = 'BETA' // RELEASE | BETA | ALPHA
def versionMarker = versionType.charAt(0)

group = 'it.italiandudes'
version = versionNumber+versionMarker

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'it.italiandudes:idl:1.0.5'
    implementation 'org.json:json:20231013'
    implementation 'club.minnced:java-discord-rpc:2.0.1'
    implementation 'net.dv8tion:JDA:5.0.0-beta.17'
    implementation 'com.github.walkyst:lavaplayer-fork:1.4.3'
    implementation 'ch.qos.logback:logback-classic:1.4.12'
    implementation 'org.xerial:sqlite-jdbc:3.43.0.0'
}

shadowJar {
    manifest {
        attributes "Main-Class" : "it.italiandudes.bot6329.Bot6329"
        attributes "Title" : "Bot6329"
        attributes "Vendor" : "Hacker6329"
        attributes "Version" : archiveVersion.get()
        attributes "Version-Number" : archiveVersion.get()
        attributes "Version-Type" : versionType
        attributes "Version-Marker" : versionMarker
        attributes "Timestamp" : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
    archiveFileName = getRootDir().getName()+"-"+archiveVersion.get()+".jar"
}

tasks.register('buildJar') {
    dependsOn shadowJar
    group = "Build"
    description = "Build and assemble the Bot6329 jar"
}

tasks.register('runBot', Exec) {
    dependsOn shadowJar
    group = "Execution"
    description = "Build, assemble and run Bot6329"
    standardInput = System.in
    commandLine "java", "-jar", jar.archiveFile.get()
}