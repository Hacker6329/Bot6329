plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

// Versioning
def versionNumber = '1.4.9' // MAJOR.MINOR.PATCH.DEV
def versionType = 'RELEASE' // RELEASE | BETA | ALPHA | DEV
def versionMarker = versionType.charAt(0)

group = 'it.italiandudes'
version = versionNumber+versionMarker

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url "https://m2.chew.pro/snapshots"
    }
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.1.0'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'commons-codec:commons-codec:1.17.0'
    implementation 'it.italiandudes:idl:1.0.5'
    implementation 'org.json:json:20231013'
    implementation 'club.minnced:java-discord-rpc:2.0.1'
    implementation 'net.dv8tion:JDA:5.1.2'
    implementation 'pw.chew:jda-chewtils:2.0-SNAPSHOT'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'org.xerial:sqlite-jdbc:3.46.0.0'
    implementation 'dev.arbjerg:lavaplayer:2.2.3'
    implementation 'com.grack:nanojson:1.9'
    implementation files('lib/youtube-common-1.11.5.jar', 'lib/youtube-v2-1.11.5.jar')
}

shadowJar {
    manifest {
        attributes "Main-Class" : "it.italiandudes.bot6329.Bot6329"
        attributes "Title" : "Bot6329"
        attributes "Vendor" : "Hacker6329"
        attributes "Version" : archiveVersion.get()
        attributes "Version-Number" : archiveVersion.get()
        attributes "Version-Type" : versionType
        attributes "Version-Marker" : versionMarker
        attributes "Timestamp" : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
    archiveFileName = getRootDir().getName()+"-"+archiveVersion.get()+".jar"
}

tasks.register('buildJar') {
    dependsOn shadowJar
    group = "Build"
    description = "Build and assemble the Bot6329 jar"
}

tasks.register('runBot', Exec) {
    dependsOn shadowJar
    group = "Execution"
    description = "Build, assemble and run Bot6329"
    standardInput = System.in
    commandLine "java", "-jar", jar.archiveFile.get()
}